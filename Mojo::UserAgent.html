<!DOCTYPE html>
<html>
  <head>
    <!-- meta -->
<meta charset="UTF-8">
<meta name="viewport" content="width=device-width,initial-scale=1.0,minimum-scale=1.0">
<link rel="icon" type="image/x-icon" href="/images/mojodoc-logo.png">
<link rel="stylesheet" type="text/css" href="/css/common.css">

<script type="text/javascript" src="/js/jquery-1.9.0.min.js"></script>
<script type="text/javascript" src="/js/google-code-prettify/prettify.js"></script>
<link  type="text/css" rel="stylesheet" href="/js/google-code-prettify/prettify.css"/>

<script>
  $(function(){
    // google code prettifyの有効化
    $("pre").addClass("prettyprint");
    function init(event){
      prettyPrint();
    }
    if(window.addEventListener)window.addEventListener("load",init,false);
    else if(window.attachEvent)window.attachEvent("onload",init);
  });
</script>

<title>Mojo::UserAgent - ノンブロッキングI/OのHTTPとWebSocketのユーザーエージェント - Mojoliciousドキュメント 日本語訳</title>
<meta name="description" content="Mojolicious API リファレンス">
  </head>
  <body>
    <div class="container">
      <div class="header">
        <!-- header -->
<div class="main">
  <h1>
    <a href="/">Mojoliciousドキュメント 日本語訳</a>
  </h1>
</div>

      </div>
      <div class="main">
        <div class="content">
          <div class="entry">
  <div class="top">
    <!-- top -->

  </div>
  <div class="middle">
    <html><head><title>名前</title>
<meta http-equiv="Content-Type" content="text/html; charset=ISO-8859-1" >
</head>
<body class='pod'>
<!--
  generated by Pod::Simple::HTML v3.40,
  using Pod::Simple::PullParser v3.40,
  under Perl v5.028000 at Sat Apr 11 03:55:05 2020 GMT.

 If you want to change this HTML document, you probably shouldn't do that
   by changing it directly.  Instead, see about changing the calling options
   to Pod::Simple::HTML, and/or subclassing Pod::Simple::HTML,
   then reconverting this document from the Pod source.
   When in doubt, email the author of Pod::Simple::HTML for advice.
   See 'perldoc Pod::Simple::HTML' for more info.

-->

<!-- start doc -->
<a name='___top' class='dummyTopAnchor' ></a>

<p><a href="/mojo-api-reference.html">Mojolicious API リファレンス</a></p>

<h2><a href="/Mojo::UserAgent.html">Mojo::UserAgent - ノンブロッキングI/OのHTTPとWebSocketのユーザーエージェント</a></p>

<h3><a class='u'
name="_"
>使い方</a></h3>

<pre>use Mojo::UserAgent;

# 良い粒度のレスポンスの処理(接続エラーの場合は、例外を発生)
my $ua  = Mojo::UserAgent->new;
my $res = $ua->get('mojolicious.org/perldoc')->result;
if    ($res->is_success)  { say $res->body }
elsif ($res->is_error)    { say $res->message }
elsif ($res->code == 301) { say $res->headers->location }
else                      { say 'Whatever...' }

# Acceptヘッダをつけてユニコードのスノーマンにハローを言う
say $ua->get('www.☃.net?hello=there' => {Accept => '*/*'})->result->body;

# HTMLとXMLのリソースからCSSセレクタを使ってデータを抽出する
say $ua->get('www.perl.org')->result->dom->at('title')->text;

# 新しいサイトから最近のヘッドラインをスクレイピングする
say $ua->get('blogs.perl.org')
  ->result->dom->find('h2 > a')->map('text')->join("\n");

# コンテンツのついたIPv6のPUTリクエスト
my $tx = $ua->put('[::1]:3000' => {'Content-Type' => 'text/plain'} => 'Hi!');

# ベーシック認証によるすばやいJSON APIリクエスト
my $value = $ua->get('https://sri:t3st@example.com/test.json')->result->json;

# TLS証明書認証のついたJSON POST (application/json)
my $tx = $ua->cert('tls.crt')->key('tls.key')
  ->post('https://example.com' => json => {top => 'secret'});

# POSTフォーム (application/x-www-form-urlencoded)
my $tx = $ua->post('https://metacpan.org/search' => form => {q => 'mojo'});

# DuckDuckGoのanonymously through Torを検索
$ua->proxy->http('socks://127.0.0.1:9050');
say $ua->get('api.3g2upl4pq6kufc4m.onion/?q=mojolicious&format=json')
  ->result->json('/Abstract');

# UNIXドメインソケットを通したGETリクエスト"/tmp/myapp.sock" (スラッシュをパーセントエンコード)
say $ua->get('http+unix://%2Ftmp%2Fmyapp.sock/perldoc')->result->body;

# GitHubからMojoliciousをダウンロードするためにリダイレクトを設定
$ua->max_redirects(5)
  ->get('https://www.github.com/mojolicious/mojo/tarball/master')
  ->result->save_to('/home/sri/mojo.tar.gz');

# ノンブロッキングリクエスト
$ua->get('mojolicious.org' => sub {
  my ($ua, $tx) = @_;
  say $tx->result->dom->at('title')->text;
});
Mojo::IOLoop->start unless Mojo::IOLoop->is_running;

# ノンブロッキングな並列リクエスト(プロミスで同期)
my $mojo = $ua->get_p('mojolicious.org');
my $cpan = $ua->get_p('cpan.org');
Mojo::Promise->all($mojo, $cpan)->then(sub {
  my ($mojo, $cpan) = @_;
  say $mojo->[0]->result->dom->at('title')->text;
  say $cpan->[0]->result->dom->at('title')->text;
})->wait;

# UNIXドメインソケットを通してJSONメッセージを受信・送信するためのノンブロッキングなWebSocket接続
$ua->websocket('ws+unix://%2Ftmp%2Fmyapp.sock/echo.json' => sub {
  my ($ua, $tx) = @_;
  say 'WebSocket handshake failed!' and return unless $tx->is_websocket;
  $tx->on(json => sub {
    my ($tx, $hash) = @_;
    say "WebSocket message via JSON: $hash->{msg}";
    $tx->finish;
  });
  $tx->send({json => {msg => 'Hello World!'}});
});
Mojo::IOLoop->start unless Mojo::IOLoop->is_running;</pre>

<h3><a class='u'
name="_"
>説明</a></h3>

<p><a href="/Mojo::UserAgent.html">Mojo::UserAgent</a> は完全な特徴を備えたノンブロッキングI/OのHTTPと WebSocketのユーザーエージェントです。 IPv6, TLS, SNI, IDNA, HTTP/SOCKS5プロキシ、Unixドメインソケット Comet (ロングポーリング), キープアライブ, コネクションポーリング, タイムアウト, クッキー、マルチパート、プロシキ、gzip圧縮、複数のイベントループをサポートしています。</p>

<p>すべての接続は、新しいプロセスがフォークされれば、自動的にリセットされます。 これは複数のプロセスが、同じ<a href="/Mojo::UserAgent.html">Mojo::UserAgent</a>オブジェクトを安全に共有する ことを許可します。</p>

<p>よりよいスケーラビリティ(epoll, kqueue)、ノンブロッキングの名前解決、 SOCK5、TLSサポートも以下のモジュールによってサポートされます。 オプションのモジュールとして<a href="https://metacpan.org/pod/EV" class="podlinkpod"
>EV</a>(4.0+), <a href="https://metacpan.org/pod/Net%3A%3ADNS%3A%3ANative" class="podlinkpod"
>Net::DNS::Native</a> (0.15+) <a href="https://metacpan.org/pod/IO%3A%3ASocket%3A%3ASSL" class="podlinkpod"
>IO::Socket::SSL</a>(2.009+),<a href="https://metacpan.org/pod/IO%3A%3ASocket%3A%3ASocks" class="podlinkpod"
>IO::Socket::Socks</a> (0.64+)、<a href="https://metacpan.org/pod/IO%3A%3ASocket%3A%3ASSL" class="podlinkpod"
>IO::Socket::SSL</a> (1.94+)が 透過的にサポートされ、インストールされていれば利用されます。 個々の機能は<code>MOJO_NO_NDN</code>、<code>MOJO_NO_IPV6</code>と<code>MOJO_NO_TLS</code>環境変数で無効にすることもできます。</p>

<h4><a class='u'
name="_"
>イベント</a></h4>

<p><a href="/Mojo::UserAgent.html">Mojo::UserAgent</a>は<a href="/Mojo::EventEmitter.html">Mojo::EventEmitter</a>を継承しており、次のイベントが発生します。</p>

<h4><a class='u'
name="prepare"
>prepare</a></h4>

<pre>$ua->on(prepare => sub {
  my ($ua, $tx) = @_;
  ...
});</pre>

<p>新しいトランザクションが準備されているときはいつでも、相対URLが書き換えられ、クッキーが追加される前に発行されます。 これは自動的に準備されたプロキシの<code>CONNECT</code>リクエストとそれに続くリダイレクトを含みます</p>

<pre>$ua->on(prepare => sub {
  my ($ua, $tx) = @_;
  $tx->req->url(Mojo::URL->new('/mock-mojolicious'))
    if $tx->req->url->host eq 'mojolicious.org';
});</pre>

<h4><a class='u'
name="start"
>start</a></h4>

<pre>$ua->on(start => sub {
  my ($ua, $tx) = @_;
  ...
});</pre>

<p>新しいトランザクションがまさに開始しようとしているときにいつでも発生します。 これは自動的に準備されるプロキシの<code>CONNECT</code>リクエストとレスポンス、それに続くリダイレクトも含みます。</p>

<pre>$ua->on(start => sub {
  my ($ua, $tx) = @_;
  $tx->req->headers->header('X-Bender', 'Bite my shiny metal ass!');
});</pre>

<h3><a class='u'
name="_"
>属性</a></h3>

<p><a href="/Mojo::UserAgent.html">Mojo::UserAgent</a> は以下の属性を実装しています。</p>

<h4><a class='u'
name="ca"
>ca</a></h4>

<pre>my $ca = $ua->ca;
$ua    = $ua->ca('/etc/tls/ca.crt');</pre>

<p>TLS認証局ファイルへのパス。 デフォルトは<code>MOJO_CA_FILE</code>環境変数の値。 ホスト名の検証も有効にします。</p>

<pre># デバッグで認証局を表示する
IO::Socket::SSL::set_ctx_defaults(
  SSL_verify_callback => sub { say "Authority: $_[2]" and return $_[0] });</pre>

<h4><a class='u'
name="cert"
>cert</a></h4>

<pre>my $cert = $ua->cert;
$ua      = $ua->cert('tls.crt');</pre>

<p>TLS証明書ファイルへのパス。デフォルトは <code>MOJO_CERT_FILE</code> の値です。</p>

<h4><a class='u'
name="connect_timeout"
>connect_timeout</a></h4>

<pre>my $timeout = $ua->connect_timeout;
$ua         = $ua->connect_timeout(5);</pre>

<p>接続が確立されるまでの最大秒数。デフォルトは<code>MOJO_CONNECT_TIMEOUT</code>環境変数 の値か<code>10</code>です。</p>

<h4><a class='u'
name="cookie_jar"
>cookie_jar</a></h4>

<pre>my $cookie_jar = $ua->cookie_jar;
$ua            = $ua->cookie_jar(Mojo::CookieJar->new);</pre>

<p>このユーザーエージェントのリクエストのためのクッキージャー。 デフォルトは <a href="/Mojo::CookieJar.html">Mojo::CookieJar</a> オブジェクトです。</p>

<pre># すべてのクッキーを無効にする
$ua->cookie_jar->ignore(sub { 1 });

# パブリックなサフィックスのためにクッキーを無視する
my $ps = IO::Socket::SSL::PublicSuffix->default;
$ua->cookie_jar->ignore(sub {
  my $cookie = shift;
  return undef unless my $domain = $cookie->domain;
  return ($ps->public_suffix($domain))[0] eq '';
});

# ジャーにカスタムクッキーを追加する
$ua->cookie_jar->add(
  Mojo::Cookie::Response->new(
    name   => 'foo',
    value  => 'bar',
    domain => 'mojolicio.us',
    path   => '/perldoc'
  )
);</pre>

<h4><a class='u'
name="inactivity_timeout"
>inactivity_timeout</a></h4>

<pre>my $timeout = $ua->inactivity_timeout;
$ua         = $ua->inactivity_timeout(15);</pre>

<p>接続がドロップする前に非アクティブでいる最大の秒数。 デフォルトは<code>MOJO_INACTIVITY_TIMEOUT</code>環境変数の値か<code>20</code>です。 この値を<code>0</code>に設定すると接続を永久に非アクティブにしたままにすることができます。</p>

<h4><a class='u'
name="insecure"
>insecure</a></h4>

<pre>my $bool = $ua->insecure;
$ua      = $ua->insecure($bool);</pre>

<p>Do not require a valid TLS certificate to access HTTPS/WSS sites, defaults to the value of the <code>MOJO_INSECURE</code> environment variable.</p>

<p>HTTPS / WSSサイトにアクセスするために有効なTLS証明書を必要としません。デフォルトは <code>MOJO_INSECURE</code>環境変数の値です。</p>

<pre># テスト用にTLS証明書の検証を無効にする
say $ua->insecure(1)->get('https://127.0.0.1:3000')->result->code;</pre>

<h4><a class='u'
name="ioloop"
>ioloop</a></h4>

<pre>my $loop = $ua->ioloop;
$ua      = $ua->ioloop(Mojo::IOLoop->new);</pre>

<p>I/O 処理のために利用されるループオブジェクト。デフォルトは <a href="/Mojo::IOLoop.html">Mojo::IOLoop</a> オブジェクトが利用されます。</p>

<h4><a class='u'
name="key"
>key</a></h4>

<pre>my $key = $ua->key;
$ua     = $ua->key('/etc/tls/client.crt');</pre>

<p>TLS キーファイルへのパス。デフォルトは <code>MOJO_KEY_FILE</code>環境変数の値です。</p>

<h4><a class='u'
name="local_address"
>local_address</a></h4>

<pre>my $address = $ua->local_address;
$ua         = $ua->local_address('127.0.0.1');</pre>

<p>バインドされているローカルアドレス。</p>

<h4><a class='u'
name="max_connections"
>max_connections</a></h4>

<pre>my $max_connections = $ua->max_connections;
$ua                 = $ua->max_connections(5);</pre>

<p>ユーザーエージェントが、最も古いキャッシュされた接続を閉じ始めるまでに 保有するキープアライブ接続の最大数。デフォルトは <code>5</code>。この値を<code>0</code>に設定するとすべての接続がケプトアライブになることを防ぎます。</p>

<h4><a class='u'
name="max_redirects"
>max_redirects</a></h4>

<pre>my $max_redirects = $ua->max_redirects;
$ua               = $ua->max_redirects(3);</pre>

<p>ユーザーエージェントが、失敗する前までに辿るリダイレクトの最大数。 デフォルトは <code>MOJO_MAX_REDIRECTS</code> の値、または <code>0</code> です。</p>

<h4><a class='u'
name="max_response_size"
>max_response_size</a></h4>

<pre>my $max = $ua->max_response_size;
$ua     = $ua->max_response_size(16777216);</pre>

<p>最大応答サイズ（バイト）。デフォルトは<a href="/Mojo::Message::Response.html">Mojo::Message::Response</a>の"max_message_size"の値です。 この値を<code>0</code>に設定すると、無限のサイズのレスポンスを許可します。 この値を増加させると、劇的にメモリの使用量が増えることに注意してください。たとえば、とても大きなサイズのレスポンスボディを、<a href="/Mojo::Message.html">Mojo::Message</a>の"dom"や"json"で解析しようとした場合などです。</p>

<h4><a class='u'
name="proxy"
>proxy</a></h4>

<pre>my $proxy = $ua->proxy;
$ua       = $ua->proxy(Mojo::UserAgent::Proxy->new);</pre>

<p>プロキシマネージャー。デフォルトは<a href="/Mojo::UserAgent::Proxy.html">Mojo::UserAgent::Proxy</a>オブジェクト。</p>

<pre># 環境からプロキシサーバーを検知します。
$ua->proxy->detect;

# 手動でHTTPプロキシを設定(HTTPS/WebSocketsのためにCONNECTを使う)
$ua->proxy->http('http://127.0.0.1:8080')->https('http://127.0.0.1:8080');

# Tor(SOCKS5)のための手動での設定
$ua->proxy->http('socks://127.0.0.1:9050')->https('socks://127.0.0.1:9050');

# 手動でUNIXドメインソケットを設定する(HTTPS/WebSocketsのためにCONNECTを使います)
$ua->proxy->http('http+unix://%2Ftmp%2Fproxy.sock')
  ->https('http+unix://%2Ftmp%2Fproxy.sock');</pre>

<h4><a class='u'
name="request_timeout"
>request_timeout</a></h4>

<pre>my $timeout = $ua->request_timeout;
$ua         = $ua->request_timeout(5);</pre>

<p>「接続が確立、リクエストが送信、完全なレスポンスを受けとる」ことがキャンセルされるまでの 最大秒数。デフォルトは、<code>MOJO_REQUEST_TIMEOUT</code>環境変数の値か<code>0</code>。 この値を<code>0</code>に設定すると、ユーザーエージェントが無限に待つことを許可します。 タイムアウトはすべての続くリダイレクトにおいてリセットされます。</p>

<pre># 全体の制限は5秒、接続には3秒が費やされるかもしれない。
$ua->max_redirects(0)->connect_timeout(3)->request_timeout(5);</pre>

<h4><a class='u'
name="server"
>server</a></h4>

<pre>my $server = $ua->server;
$ua        = $ua->server(Mojo::UserAgent::Server->new);</pre>

<p>アプリケーションサーバーの相対URLが一緒に処理されます。 デフォルトは<a href="/Mojo::UserAgent::Server.html">Mojo::UserAgent::Server</a>です。</p>

<pre># Webサービスをモックする
$ua->server->app(Mojolicious->new);
$ua->server->app->routes->get('/time' => sub {
  my $c = shift;
  $c->render(json => {now => time});
});
my $time = $ua->get('/time')->result->json->{now};

# ログレベルの変更
$ua->server->app->log->level('fatal');

# 相対URLのブロッキングを処理するために現在利用されているポート
say $ua->server->url->port;

# 相対URLのノンブロッキングを処理するために現在利用されているポート
say $ua->server->nb_url->port;</pre>

<h4><a class='u'
name="transactor"
>transactor</a></h4>

<pre>my $t = $ua->transactor;
$ua   = $ua->transactor(Mojo::UserAgent::Transactor->new);</pre>

<p>トランザクションビルダー。 デフォルトは<a href="/Mojo::UserAgent::Transactor.html">Mojo::UserAgent::Transactor</a>オブジェクトです。</p>

<pre># ユーザーエージェントの名前を変更
$ua->transactor->name('MyUA 1.0');

# 圧縮を無効にする
$ua->transactor->compressed(0);</pre>

<h3><a class='u'
name="_"
>メソッド</a></h3>

<p><a href="/Mojo::UserAgent.html">Mojo::UserAgent</a> は <a href="/Mojo::EventEmitter.html">Mojo::EventEmitter</a> のすべてのメソッドを継承しており、 以下の新しいメソッドを実装しています。</p>

<h4><a class='u'
name="build_tx"
>build_tx</a></h4>

<pre>my $tx = $ua->build_tx(GET => 'example.com');
my $tx = $ua->build_tx(
  PUT => 'http://example.com' => {Accept => '*/*'} => 'Content!');
my $tx = $ua->build_tx(
  PUT => 'http://example.com' => {Accept => '*/*'} => form => {a => 'b'});
my $tx = $ua->build_tx(
  PUT => 'http://example.com' => {Accept => '*/*'} => json => {a => 'b'});</pre>

<p><a href="/Mojo::UserAgent::Transactor.html">Mojo::UserAgent::Transactor</a>の<code>tx</code>メソッドで <a href="/Mojo::Transaction::HTTP.html">Mojo::Transaction::HTTP</a>オブジェクトを生成します。</p>

<pre># カスタムクッキーを持ったリクエスト
my $tx = $ua->build_tx(GET => 'https://example.com/account');
$tx->req->cookies({name => 'user', value => 'sri'});
$tx = $ua->start($tx);

# gzip圧縮を非アクティブにする
my $tx = $ua->build_tx(GET => 'example.com');
$tx->req->headers->remove('Accept-Encoding');
$tx = $ua->start($tx);

# ひとつのエラーを発生することによって、レスポンスを中断する
my $tx = $ua->build_tx(GET => 'example.com');
$tx->res->on(progress => sub {
  my $res = shift;
  return unless my $server = $res->headers->server;
  $res->error({message => 'Oh noes, it is IIS!'}) if $server =~ /IIS/;
});
$tx = $ua->start($tx);</pre>

<h4><a class='u'
name="build_websocket_tx"
>build_websocket_tx</a></h4>

<pre>my $tx = $ua->build_websocket_tx('ws://example.com');
my $tx = $ua->build_websocket_tx(
  'ws://example.com' => {DNT => 1} => ['v1.proto']);</pre>

<p><a href="/Mojo::UserAgent::Transactor.html">Mojo::UserAgent::Transactor</a>の<code>websocket</code>メソッドで <a href="/Mojo::Transaction::HTTP.html">Mojo::Transaction::HTTP</a>オブジェクトを生成します。</p>

<pre># クッキーを持ったWebSocketハンドシェイク
my $tx = $ua->build_websocket_tx('wss://example.com/echo');
$tx->req->cookies({name => 'user', value => 'sri'});
$ua->start($tx => sub {
  my ($ua, $tx) = @_;
  say 'WebSocket handshake failed!' and return unless $tx->is_websocket;
  $tx->on(message => sub {
    my ($tx, $msg) = @_;
    say "WebSocket message: $msg";
    $tx->finish;
  });
  $tx->send('Hi!');
});
Mojo::IOLoop->start unless Mojo::IOLoop->is_running;</pre>

<h4><a class='u'
name="delete"
>delete</a></h4>

<pre>my $tx = $ua->delete('example.com');
my $tx = $ua->delete('http://example.com' => {Accept => '*/*'} => 'Hi!');
my $tx = $ua->delete(
  'http://example.com' => {Accept => '*/*'} => form => {a => 'b'});
my $tx = $ua->delete(
  'http://example.com' => {Accept => '*/*'} => json => {a => 'b'});</pre>

<p>ブロッキングする<code>DELETE</code>リクエストを実行し、結果である<a href="/Mojo::Transaction::HTTP.html">Mojo::Transaction::HTTP</a>オブジェクト を返却します。 (メソッドを除いて)<a href="/Mojo::UserAgent::Transactor.html">Mojo::UserAgent::Transactor</a>の<code>tx</code>とまったく同じ引数を受け取ります。 ノンブロッキングのリクエストを実行するため、コールバックを追加することも できます。</p>

<pre>$ua->delete('http://example.com' => json => {a => 'b'} => sub {
  my ($ua, $tx) = @_;
  say $tx->result->body;
});
Mojo::IOLoop->start unless Mojo::IOLoop->is_running;</pre>

<h4><a class='u'
name="delete_p"
>delete_p</a></h4>

<pre>my $promise = $ua->delete_p('http://example.com');</pre>

<p>Same as <a href="#delete" class="podlinkpod"
>"delete"</a>, but performs all requests non-blocking and returns a <a href="https://metacpan.org/pod/Mojo%3A%3APromise" class="podlinkpod"
>Mojo::Promise</a> object instead of accepting a callback.</p>

<p>"delete"と同じですが、すべてのリクエストをノンブロッキングで実行し、 コールバックを受け付ける代わりに、<a href="/Mojo::Promise.html">Mojo::Promise</a>オブジェクトを返します。</p>

<pre>$ua->delete_p('http://example.com' => json => {a => 'b'})->then(sub {
  my $tx = shift;
  say $tx->result->body;
})->catch(sub {
  my $err = shift;
  warn "Connection error: $err";
})->wait;</pre>

<h4><a class='u'
name="get"
>get</a></h4>

<pre>my $tx = $ua->get('example.com');
my $tx = $ua->get('http://example.com' => {Accept => '*/*'} => 'Hi!');
my $tx = $ua->get(
  'http://example.com' => {Accept => '*/*'} => form => {a => 'b'});
my $tx = $ua->get(
  'http://example.com' => {Accept => '*/*'} => json => {a => 'b'});</pre>

<p>ブロッキングするHTTP <code>GET</code>リクエストを実行し、結果である<a href="/Mojo::Transaction::HTTP.html">Mojo::Transaction::HTTP</a>オブジェクト を返却します。 (メソッドを除いて)<a href="/Mojo::UserAgent::Transactor.html">Mojo::UserAgent::Transactor</a>の<code>tx</code>とまったく同じ引数を受け取ります。 ノンブロッキングのリクエストを実行するため、コールバックを追加することも できます。</p>

<pre>$ua->get('http://example.com' => json => {a => 'b'} => sub {
  my ($ua, $tx) = @_;
  say $tx->result->body;
});
Mojo::IOLoop->start unless Mojo::IOLoop->is_running;</pre>

<h4><a class='u'
name="get_p"
>get_p</a></h4>

<pre>my $promise = $ua->get_p('http://example.com');</pre>

<p>"get"と同じですが、すべてのリクエストをノンブロッキングで実行し、 コールバックを受け付ける代わりに、<a href="/Mojo::Promise.html">Mojo::Promise</a>オブジェクトを返します。</p>

<pre>$ua->get_p('http://example.com' => json => {a => 'b'})->then(sub {
  my $tx = shift;
  say $tx->result->body;
})->catch(sub {
  my $err = shift;
  warn "Connection error: $err";
})->wait;</pre>

<h4><a class='u'
name="head"
>head</a></h4>

<pre>my $tx = $ua->head('example.com');
my $tx = $ua->head('http://example.com' => {Accept => '*/*'} => 'Hi!');
my $tx = $ua->head(
  'http://example.com' => {Accept => '*/*'} => form => {a => 'b'});
my $tx = $ua->head(
  'http://example.com' => {Accept => '*/*'} => json => {a => 'b'});</pre>

<p>ブロッキングするHTTP <code>HEAD</code>リクエストを実行し、結果である<a href="/Mojo::Transaction::HTTP.html">Mojo::Transaction::HTTP</a>オブジェクト を返却します。 (メソッドを除いて)<a href="/Mojo::UserAgent::Transactor.html">Mojo::UserAgent::Transactor</a>の<code>tx</code>とまったく同じ引数を受け取ります。 ノンブロッキングのリクエストを実行するため、コールバックを追加することも できます。</p>

<pre>$ua->head('http://example.com' => json => {a => 'b'} => sub {
  my ($ua, $tx) = @_;
  say $tx->result->body;
});
Mojo::IOLoop->start unless Mojo::IOLoop->is_running;</pre>

<h4><a class='u'
name="head_p"
>head_p</a></h4>

<pre>my $promise = $ua->head_p('http://example.com');</pre>

<p>"head"と同じですが、すべてのリクエストをノンブロッキングで実行し、 コールバックを受け付ける代わりに、<a href="/Mojo::Promise.html">Mojo::Promise</a>オブジェクトを返します。</p>

<pre>$ua->head_p('http://example.com' => json => {a => 'b'})->then(sub {
  my $tx = shift;
  say $tx->result->body;
})->catch(sub {
  my $err = shift;
  warn "Connection error: $err";
})->wait;</pre>

<h4><a class='u'
name="options"
>options</a></h4>

<pre>my $tx = $ua->options('example.com');
my $tx = $ua->options('http://example.com' => {Accept => '*/*'} => 'Hi!');
my $tx = $ua->options(
  'http://example.com' => {Accept => '*/*'} => form => {a => 'b'});
my $tx = $ua->options(
  'http://example.com' => {Accept => '*/*'} => json => {a => 'b'});</pre>

<p>ブロッキングする<code>OPTIONS</code>リクエストを実行し、結果である<a href="/Mojo::Transaction::HTTP.html">Mojo::Transaction::HTTP</a>オブジェクト を返却します。 (メソッドを除いて)<a href="/Mojo::UserAgent::Transactor.html">Mojo::UserAgent::Transactor</a>の<code>tx</code>とまったく同じ引数を受け取ります。 ノンブロッキングのリクエストを実行するため、コールバックを追加することも できます。</p>

<pre>$ua->options('http://example.com' => json => {a => 'b'} => sub {
  my ($ua, $tx) = @_;
  say $tx->result->body;
});
Mojo::IOLoop->start unless Mojo::IOLoop->is_running;</pre>

<h4><a class='u'
name="options_p"
>options_p</a></h4>

<pre>my $promise = $ua->options_p('http://example.com');</pre>

<p>"options"と同じですが、すべてのリクエストをノンブロッキングで実行し、 コールバックを受け付ける代わりに、<a href="/Mojo::Promise.html">Mojo::Promise</a>オブジェクトを返します。</p>

<pre>$ua->options_p('http://example.com' => json => {a => 'b'})->then(sub {
  my $tx = shift;
  say $tx->result->body;
})->catch(sub {
  my $err = shift;
  warn "Connection error: $err";
})->wait;</pre>

<h4><a class='u'
name="patch"
>patch</a></h4>

<pre>my $tx = $ua->patch('example.com');
my $tx = $ua->patch('http://example.com' => {Accept => '*/*'} => 'Hi!');
my $tx = $ua->patch(
  'http://example.com' => {Accept => '*/*'} => form => {a => 'b'});
my $tx = $ua->patch(
  'http://example.com' => {Accept => '*/*'} => json => {a => 'b'});</pre>

<p>ブロッキングする<code>PATCH</code>リクエストを実行し、結果である<a href="/Mojo::Transaction::HTTP.html">Mojo::Transaction::HTTP</a>オブジェクト を返却します。 (メソッドを除いて)<a href="/Mojo::UserAgent::Transactor.html">Mojo::UserAgent::Transactor</a>の<code>tx</code>とまったく同じ引数を受け取ります。 ノンブロッキングのリクエストを実行するため、コールバックを追加することも できます。</p>

<pre>$ua->patch('http://example.com' => json => {a => 'b'} => sub {
  my ($ua, $tx) = @_;
  say $tx->result->body;
});
Mojo::IOLoop->start unless Mojo::IOLoop->is_running;</pre>

<h4><a class='u'
name="patch_p"
>patch_p</a></h4>

<pre>my $promise = $ua->patch_p('http://example.com');</pre>

<p>"patch"と同じですが、すべてのリクエストをノンブロッキングで実行し、 コールバックを受け付ける代わりに、<a href="/Mojo::Promise.html">Mojo::Promise</a>オブジェクトを返します。</p>

<pre>$ua->patch_p('http://example.com' => json => {a => 'b'})->then(sub {
  my $tx = shift;
  say $tx->result->body;
})->catch(sub {
  my $err = shift;
  warn "Connection error: $err";
})->wait;</pre>

<h4><a class='u'
name="post"
>post</a></h4>

<pre>my $tx = $ua->post('example.com');
my $tx = $ua->post('http://example.com' => {Accept => '*/*'} => 'Hi!');
my $tx = $ua->post(
  'http://example.com' => {Accept => '*/*'} => form => {a => 'b'});
my $tx = $ua->post(
  'http://example.com' => {Accept => '*/*'} => json => {a => 'b'});</pre>

<p>ブロッキングする<code>POST</code>リクエストを実行し、結果である<a href="/Mojo::Transaction::HTTP.html">Mojo::Transaction::HTTP</a>オブジェクト を返却します。 (メソッドを除いて)<a href="/Mojo::UserAgent::Transactor.html">Mojo::UserAgent::Transactor</a>の<code>tx</code>とまったく同じ引数を受け取ります。 ノンブロッキングのリクエストを実行するため、コールバックを追加することも できます。</p>

<pre>$ua->post('http://example.com' => json => {a => 'b'} => sub {
  my ($ua, $tx) = @_;
  say $tx->result->body;
});
Mojo::IOLoop->start unless Mojo::IOLoop->is_running;</pre>

<h4><a class='u'
name="post_p"
>post_p</a></h4>

<pre>my $promise = $ua->post_p('http://example.com');</pre>

<p>"post"と同じですが、すべてのリクエストをノンブロッキングで実行し、 コールバックを受け付ける代わりに、<a href="/Mojo::Promise.html">Mojo::Promise</a>オブジェクトを返します。</p>

<pre>$ua->post_p('http://example.com' => json => {a => 'b'})->then(sub {
  my $tx = shift;
  say $tx->result->body;
})->catch(sub {
  my $err = shift;
  warn "Connection error: $err";
})->wait;</pre>

<h4><a class='u'
name="put"
>put</a></h4>

<pre> my $tx = $ua->put('example.com');
my $tx = $ua->put('http://example.com' => {Accept => '*/*'} => 'Hi!');
my $tx = $ua->put(
  'http://example.com' => {Accept => '*/*'} => form => {a => 'b'});
my $tx = $ua->put(
  'http://example.com' => {Accept => '*/*'} => json => {a => 'b'});</pre>

<p>ブロッキングする<code>PUT</code>リクエストを実行し、結果である<a href="/Mojo::Transaction::HTTP.html">Mojo::Transaction::HTTP</a>オブジェクト を返却します。 (メソッドを除いて)<a href="/Mojo::UserAgent::Transactor.html">Mojo::UserAgent::Transactor</a>の<code>tx</code>とまったく同じ引数を受け取ります。 ノンブロッキングのリクエストを実行するため、コールバックを追加することも できます。</p>

<pre>$ua->put('http://example.com' => json => {a => 'b'} => sub {
  my ($ua, $tx) = @_;
  say $tx->result->body;
});
Mojo::IOLoop->start unless Mojo::IOLoop->is_running;</pre>

<h4><a class='u'
name="put_p"
>put_p</a></h4>

<pre>my $promise = $ua->put_p('http://example.com');</pre>

<p>"put"と同じですが、すべてのリクエストをノンブロッキングで実行し、 コールバックを受け付ける代わりに、<a href="/Mojo::Promise.html">Mojo::Promise</a>オブジェクトを返します。</p>

<pre>$ua->put_p('http://example.com' => json => {a => 'b'})->then(sub {
  my $tx = shift;
  say $tx->result->body;
})->catch(sub {
  my $err = shift;
  warn "Connection error: $err";
})->wait;</pre>

<h4><a class='u'
name="start"
>start</a></h4>

<pre>my $tx = $ua->start(Mojo::Transaction::HTTP->new);</pre>

<p>カスタムの<a href="/Mojo::Transaction::HTTP.html">Mojo::Transaction::HTTP</a>オブジェクトのためにブロッキングするリクエストを処理します。 ノンブロッキングのトランザクションを実行するため、コールバックを追加することも できます。</p>

<pre>my $tx = $ua->build_tx(GET => 'http://example.com');
$ua->start($tx => sub {
  my ($ua, $tx) = @_;
  say $tx->result->body;
});
Mojo::IOLoop->start unless Mojo::IOLoop->is_running;</pre>

<h4><a class='u'
name="start_p"
>start_p</a></h4>

<pre>my $promise = $ua->start_p(Mojo::Transaction::HTTP->new);</pre>

<p>"start"と同じですが、すべてのリクエストをノンブロッキングで実行し、 コールバックを受け付ける代わりに、<a href="/Mojo::Promise.html">Mojo::Promise</a>オブジェクトを返します。</p>

<pre>my $tx = $ua->build_tx(GET => 'http://example.com');
$ua->start_p($tx)->then(sub {
  my $tx = shift;
  say $tx->result->body;
})->catch(sub {
  my $err = shift;
  warn "Connection error: $err";
})->wait;</pre>

<h4><a class='u'
name="websocket"
>websocket</a></h4>

<pre>$ua->websocket('ws://example.com' => sub {...});
$ua->websocket(
  'ws://example.com' => {DNT => 1} => ['v1.proto'] => sub {...});</pre>

<p>透過的なハンドシェイクでノンブロッキングのWebSocket接続をオープンします。 <a href="/Mojo::UserAgent::Transactor.html">Mojo::UserAgent::Transactor</a>の<code>websocket</code>とまったく同じ引数を受け取ります。 コールバックは、ハンドシェイクが成功しかたに依存して、<a href="/Mojo::Transaction::WebSocket.html">Mojo::Transaction::WebSocket</a>オブジェクトか<a href="/Mojo::Transaction::HTTP.html">Mojo::Transaction::HTTP</a>オブジェクトを受け取ります。</p>

<pre>$ua->websocket('wss://example.com/echo' => ['v1.proto'] => sub {
  my ($ua, $tx) = @_;
  say 'WebSocket handshake failed!' and return unless $tx->is_websocket;
  say 'Subprotocol negotiation failed!' and return unless $tx->protocol;
  $tx->on(finish => sub {
    my ($tx, $code, $reason) = @_;
    say "WebSocket closed with status $code.";
  });
  $tx->on(message => sub {
    my ($tx, $msg) = @_;
    say "WebSocket message: $msg";
    $tx->finish;
  });
  $tx->send('Hi!');
});
Mojo::IOLoop->start unless Mojo::IOLoop->is_running;</pre>

<p><code>Sec-WebSocket-Extensions</code>を設定することによって、 <code>permessage-deflate</code>圧縮をアクティブにすることができます。 これはよりよりパフォーマンスをもたらすでしょう。 しかし、コネクションあたりメモリ使用率は300KB上昇します。</p>

<pre>$ua->websocket('ws://example.com/foo' => {
  'Sec-WebSocket-Extensions' => 'permessage-deflate'
} => sub {...});</pre>

<h4><a class='u'
name="websocket_p"
>websocket_p</a></h4>

<pre>my $promise = $ua->websocket_p('ws://example.com');</pre>

<p>"websocket"と同じですが、すべてのリクエストをノンブロッキングで実行し、 コールバックを受け付ける代わりに、<a href="/Mojo::Promise.html">Mojo::Promise</a>オブジェクトを返します。</p>

<pre>$ua->websocket_p('wss://example.com/echo')->then(sub {
  my $tx = shift;
  my $promise = Mojo::Promise->new;
  $tx->on(finish => sub { $promise->resolve });
  $tx->on(message => sub {
    my ($tx, $msg) = @_;
    say "WebSocket message: $msg";
    $tx->finish;
  });
  $tx->send('Hi!');
  return $promise;
})->catch(sub {
  my $err = shift;
  warn "WebSocket error: $err";
})->wait;</pre>

<h3><a class='u'
name="_"
>デバッグ</a></h3>

<p>いくらかの発展的な詳細な情報を<code>STDERR</code>に出力するために、 <code>MOJO_USERAGENT_DEBUG</code>環境変数を設定することができます。</p>

<pre>MOJO_USERAGENT_DEBUG=1</pre>

<h3><a class='u'
name="_"
>参考</a></h3>

<p><a href="/Mojolicious.html">Mojolicious</a>, <a href="/Mojolicious::Guides.html">Mojolicious::Guides</a>, <a href="http://mojolicio.us" class="podlinkurl"
>http://mojolicio.us</a>.</p>

<p>(Mojolicious 8.12を反映。2019年5月30日更新)</h2>

<!-- end doc -->

</body></html>

  </div>
  <div class="bottom">
    <!-- bottom -->

  </div>
</div>

        </div>
        <div class="side">
          <!-- side -->
<div class="side-list">
  <div class="side-list-title">
  　ドキュメント
  </div>
  <ul>
    <li><a href="/Mojolicious::Guides.html">Mojoliciousガイド</a></li>
    <li><a href="/mojo-api-reference.html">Mojolicious API リファレンス</a></li>
  </ul>
</div>

        </div>
      </div>
      <div class="footer">
        <div class="kimoto_system_link">
  <a href="https://kimoto-system.co.jp">
    <img src="https://kimoto-system.co.jp/images/kimoto-system-logo.png" alt="Perl Webシステム開発">
    Perl Webシステム開発の木本システム
  </a>
</div>

<div class="copyright">
  Copyright © Yuki Kimoto
</div>

      </div>
    </div>
  </body>
</html>
