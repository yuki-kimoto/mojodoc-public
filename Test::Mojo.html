<!DOCTYPE html>
<html>
  <head>
    <!-- meta -->
<meta charset="UTF-8">
<meta name="viewport" content="width=device-width,initial-scale=1.0,minimum-scale=1.0">
<link rel="icon" type="image/x-icon" href="/images/giblog-logo.png">
<link rel="stylesheet" type="text/css" href="/css/common.css">

<title> - Mojoliciousドキュメント 日本語訳</title>
<meta name="description" content="Mojolicious API リファレンス">
  </head>
  <body>
    <div class="container">
      <div class="header">
        <!-- header -->
<div class="main">
  <h1>
    <a href="/">Mojoliciousドキュメント 日本語訳</a>
  </h1>
</div>

      </div>
      <div class="main">
        <div class="content">
          <div class="entry">
  <div class="top">
    <!-- top -->

  </div>
  <div class="middle">
    <html><head><title>名前</title>
<meta http-equiv="Content-Type" content="text/html; charset=ISO-8859-1" >
</head>
<body class='pod'>
<!--
  generated by Pod::Simple::HTML v3.40,
  using Pod::Simple::PullParser v3.40,
  under Perl v5.028000 at Tue Apr  7 04:16:23 2020 GMT.

 If you want to change this HTML document, you probably shouldn't do that
   by changing it directly.  Instead, see about changing the calling options
   to Pod::Simple::HTML, and/or subclassing Pod::Simple::HTML,
   then reconverting this document from the Pod source.
   When in doubt, email the author of Pod::Simple::HTML for advice.
   See 'perldoc Pod::Simple::HTML' for more info.

-->

<!-- start doc -->
<a name='___top' class='dummyTopAnchor' ></a>

<p><a href="/Mojolicious API リファレンス.html">Mojolicious API リファレンス</a></p>

<h1><a href="/Test::Mojo.html"></a><a class='u'
name="_"
>名前</a></h1>

<p>Test::Mojo - Mojoの試験</p>

<h1><a class='u'
name="_"
>使い方</a></h1>

<pre>use Test::More;
use Test::Mojo;

my $t = Test::Mojo->new('MyApp');

# HTML/XML
$t->get_ok('/welcome')->status_is(200)->text_is('div#message' => 'Hello!');

# JSON
$t->post_ok('/search.json' => form => {q => 'Perl'})
  ->status_is(200)
  ->header_is('Server' => 'Mojolicious (Perl)')
  ->header_isnt('X-Bender' => 'Bite my shiny metal ass!')
  ->json_is('/results/4/title' => 'Perl rocks!');

# WebSocket
$t->websocket_ok('/echo')
  ->send_ok('hello')
  ->message_ok
  ->message_is('echo: hello')
  ->finish_ok;

done_testing();</pre>

<h1><a class='u'
name="_"
>説明</a></h1>

<p><a href="/Test::Mojo.html">Test::Mojo</a> は、<a href="/Mojo.html">Mojo</a> と <a href="/Mojolicious.html">Mojolicious</a> アプリケーションを開発する すべての人への、テストのためのヘルパーの集まりです。</p>

<p><a href="/Test::Mojo.html">Test::Mojo</a>は<a href="/Mojo::UserAgent.html">Mojo::UserAgent</a>に基づいたテストのためのユーザーエージェントです。 通常は、<a href="/Mojolicious.html">Mojolicious</a>アプリケーションをテストするために、<a href="https://metacpan.org/pod/Test%3A%3AMore" class="podlinkpod"
>Test::More</a>と 一緒に利用されます。 <a href="https://metacpan.org/pod/prove" class="podlinkpod"
>prove</a>を使って、 テストを実行することができます。</p>

<pre>$ prove -l -v
$ prove -l -v t/foo.t</pre>

<p>まだ定義されていなかった場合は、 <code>MOJO_LOG_LEVEL</code>環境変数は <code>HARNESS_IS_VERBOSE</code>環境変数の値に応じて、 <code>debug</code>か<code>fatal</code>に設定されます。 HTTPS/WSSとWebサービスの試験を簡単にするために<a href="/Mojo::UserAgent.html">Mojo::UserAgent</a>の"insecure"が"ua"のためにデフォルトで有効になります。</p>

<h1><a class='u'
name="_"
>属性</a></h1>

<p><a href="/Test::Mojo.html">Test::Mojo</a>は次の属性を実装しています。</p>

<h2><a class='u'
name="message"
>message</a></h2>

<pre>my $msg = $t->message;
$t      = $t->message([text => $bytes]);</pre>

<p>フレームとペイロードを含んだ 配列のリファレンスとして表現された現在のWebSocketメッセージ。</p>

<pre># もっと仕様の試験をする
use Mojo::JSON 'decode_json';
my $hash = decode_json $t->message->[1];
is ref $hash, 'HASH', 'right reference';
is $hash->{foo}, 'bar', 'right value';

# カスタムメッセージのテスト
$t->message([binary => $bytes])
  ->json_message_has('/foo/bar')
  ->json_message_hasnt('/bar')
  ->json_message_is('/foo/baz' => {yada => [1, 2, 3]});</pre>

<h2><a class='u'
name="success"
>success</a></h2>

<pre>my $bool = $t->success;
$t       = $t->success($bool);</pre>

<p>最後のテストが成功すれば真。</p>

<pre># Build custom tests
my $location_is = sub {
  my ($t, $value, $desc) = @_;
  $desc ||= "Location: $value";
  local $Test::Builder::Level = $Test::Builder::Level + 1;
  return $t->success(is($t->tx->res->headers->location, $value, $desc));
};
$t->get_ok('/')
  ->status_is(302)
  ->$location_is('http://mojolicio.us')
  ->or(sub { diag 'Must have been Joel!' });</pre>

<h2><a class='u'
name="tx"
>tx</a></h2>

<pre>my $tx = $t->tx;
$t     = $t->tx(Mojo::Transaction::HTTP->new);</pre>

<p>現在のトランザクション。 通常は <a href="/Mojo::Transaction::HTTP.html">Mojo::Transaction::HTTP</a>または<a href="/Mojo::Transaction::WebSocket.html">Mojo::Transaction::WebSocket</a> オブジェクトです。</p>

<pre># よりスペシフィックな試験
is $t->tx->res->json->{foo}, 'bar', 'right value';
ok $t->tx->res->content->is_multipart, 'multipart content';
is $t->tx->previous->res->code, 302, 'right status';</pre>

<h2><a class='u'
name="ua"
>ua</a></h2>

<pre>my $ua = $t->ua;
$t     = $t->ua(Mojo::UserAgent->new);</pre>

<p>テストのために利用されるユーザーエージェント。 デフォルトは<a href="/Mojo::UserAgent.html">Mojo::UserAgent</a>オブジェクトです。</p>

<pre># リダイレクトを許可
$t->ua->max_redirects(10);
$t->get_ok('/redirect')->status_is(200)->content_like(qr/redirected/);

# HTTPからHTTPSへプロトコルをスイッチ
$t->ua->server->url('https');
$t->get_ok('/secure')->status_is(200)->content_like(qr/secure/);

# ベーシック認証のリクエストに絶対URLを使う
my $url = $t->ua->server->url->userinfo('sri:secr3t')->path('/secrets.json');
$t->post_ok($url => json => {limit => 10})
  ->status_is(200)
  ->json_is('/1/content', 'Mojo rocks!');

# すべてのトランザクションをカスタマイズ (続くリダイレクトを含む)
$t->ua->on(start => sub {
  my ($ua, $tx) = @_;
  $tx->req->headers->accept_language('en-US');
});
$t->get_ok('/hello')->status_is(200)->content_like(qr/Howdy/);</pre>

<h1><a class='u'
name="_"
>メソッド</a></h1>

<p><a href="/Test::Mojo.html">Test::Mojo</a>は<a href="/Mojo::Base.html">Mojo::Base</a>のすべてのメソッドを継承しており、 次の新しいメソッドを実装しています。</p>

<h2><a class='u'
name="app"
>app</a></h2>

<pre>my $app = $t->app;
$t      = $t->app(Mojolicious->new);</pre>

<p><a href="/Mojo::UserAgent::Server.html">Mojo::UserAgent::Server</a>の<code>app</code>にアクセスします。</p>

<pre># ログレベルの変更
$t->app->log->level('fatal');

# アプリケーションを直接試験する
is $t->app->defaults->{foo}, 'bar', 'right value';
ok $t->app->routes->find('echo')->is_websocket, 'WebSocket route';
my $c = $t->app->build_controller;
ok $c->render(template => 'foo'), 'rendering was successful';
is $c->res->status, 200, 'right status';
is $c->res->body, 'Foo!', 'right content';

# アプリケーションの動きを変更
$t->app->hook(before_dispatch => sub {
  my $c = shift;
  $c->render(text => 'This request did not reach the router.')
    if $c->req->url->path->contains('/user');
});
$t->get_ok('/user')->status_is(200)->content_like(qr/not reach the router/);

# 追加の情報を抽出
my $stash;
$t->app->hook(after_dispatch => sub { $stash = shift->stash });
$t->get_ok('/hello')->status_is(200);
is $stash->{foo}, 'bar', 'right value';</pre>

<h2><a class='u'
name="content_is"
>content_is</a></h2>

<pre>$t = $t->content_is('working!');
$t = $t->content_is('working!', 'right content!');</pre>

<p><a href="/Mojo::Message.html">Mojo::Message</a>の<code>text</code>からコンテンツを取得した後に、 レスポンスのコンテンツが完全に一致するかをチェックします。</p>

<h2><a class='u'
name="content_isnt"
>content_isnt</a></h2>

<pre>$t = $t->content_isnt('working!');
$t = $t->content_isnt('working!', 'different content');</pre>

<p><code>content_is</code>の否定です。</p>

<h2><a class='u'
name="content_like"
>content_like</a></h2>

<pre>$t = $t->content_like(qr/working!/);
$t = $t->content_like(qr/working!/, 'right content!');</pre>

<p><a href="/Mojo::Message.html">Mojo::Message</a>の<code>text</code>からコンテンツを取得した後に、 レスポンスが類似マッチするかどうかをチェックします。</p>

<h2><a class='u'
name="content_type_is"
>content_type_is</a></h2>

<pre>$t = $t->content_type_is('text/html');</pre>

<p>レスポンスの <code>Content-Type</code> ヘッダが完全に一致するかをチェックします。</p>

<h2><a class='u'
name="content_type_isnt"
>content_type_isnt</a></h2>

<pre>$t = $t->content_type_isnt('text/html');
$t = $t->content_type_isnt('text/html', 'different content type');</pre>

<p><code>content_type_is</code>の否定です。</p>

<h2><a class='u'
name="content_type_like"
>content_type_like</a></h2>

<pre>$t = $t->content_type_like(qr/text/);
$t = $t->content_type_like(qr/text/, 'right content type!');</pre>

<p>レスポンスの <code>Content-Type</code> ヘッダが類似マッチするかどうかをチェックします。</p>

<h2><a class='u'
name="content_type_unlike"
>content_type_unlike</a></h2>

<pre>$t = $t->content_type_unlike(qr/text/);
$t = $t->content_type_unlike(qr/text/, 'different content type');</pre>

<p><code>content_type_like</code>の否定です。</p>

<h2><a class='u'
name="content_unlike"
>content_unlike</a></h2>

<pre>$t = $t->content_unlike(qr/working!/);
$t = $t->content_unlike(qr/working!/, 'different content');</pre>

<p><code>content_like</code>の否定です。</p>

<h2><a class='u'
name="delete_ok"
>delete_ok</a></h2>

<pre>$t = $t->delete_ok('http://example.com/foo');
$t = $t->delete_ok('/foo');
$t = $t->delete_ok('/foo' => {Accept => '*/*'} => 'Content!');
$t = $t->delete_ok('/foo' => {Accept => '*/*'} => form => {a => 'b'});
$t = $t->delete_ok('/foo' => {Accept => '*/*'} => json => {a => 'b'});</pre>

<p><code>DELETE</code> リクエストを実行し、成功するかどうかをチェックします。 <a href="/Mojo::UserAgent.html">Mojo::UserAgent</a>の<code>delete</code>と同じ引数を受け取ります。</p>

<h2><a class='u'
name="element_count_is"
>element_count_is</a></h2>

<pre>$t = $t->element_count_is('div.foo[x=y]', 5);
$t = $t->element_count_is('html body div', 30, 'thirty elements');</pre>

<p><a href="/Mojo::DOM.html">Mojo::DOM</a>の"find"を使ってCSSセレクタにマッチするHTML/XML要素の数を確認します。</p>

<h2><a class='u'
name="element_exists"
>element_exists</a></h2>

<pre>$t = $t->element_exists('div.foo[x=y]');
$t = $t->element_exists('html head title', 'has a title');</pre>

<p><a href="/Mojo::DOM.html">Mojo::DOM</a> の<code>at</code>を使って、CSS3 セレクタと最初にマッチする XML/HTML 要素が 存在するかをチェックします。</p>

<pre># 属性値のチェック
$t->get_ok('/login')
  ->element_exists('label[for=email]')
  ->element_exists('input[name=email][type=text][value*="example.com"]')
  ->element_exists('label[for=pass]')
  ->element_exists('input[name=pass][type=password]')
  ->element_exists('input[type=submit][value]');</pre>

<h2><a class='u'
name="element_exists_not"
>element_exists_not</a></h2>

<pre>$t = $t->element_exists_not('div.foo[x=y]');
$t = $t->element_exists_not('html head title', 'has no title');</pre>

<p><code>element_exists</code>の否定です。</p>

<h2><a class='u'
name="finish_ok"
>finish_ok</a></h2>

<pre>$t = $t->finish_ok;
$t = $t->finish_ok(1000);
$t = $t->finish_ok(1003 => 'Cannot accept data!');</pre>

<p>WebSocket接続が終了しているか。</p>

<h2><a class='u'
name="finished_ok"
>finished_ok</a></h2>

<pre>$t = $t->finished_ok(1000);</pre>

<p>Webソケット接続が緩やかに閉じるまで待ち、ステータスをチェックします。</p>

<h2><a class='u'
name="get_ok"
>get_ok</a></h2>

<pre>$t = $t->get_ok('http://example.com/foo');
$t = $t->get_ok('/foo');
$t = $t->get_ok('/foo' => {Accept => '*/*'} => 'Content!');
$t = $t->get_ok('/foo' => {Accept => '*/*'} => form => {a => 'b'});
$t = $t->get_ok('/foo' => {Accept => '*/*'} => json => {a => 'b'});</pre>

<p><code>GET</code> リクエストを実行し、成功するかどうかをチェックします。 <a href="/Mojo::UserAgent.html">Mojo::UserAgent</a>の<code>get</code>と同じ引数を受け取ります。</p>

<pre># リモートホストに対して、試験を実行
$t->get_ok('http://mojolicio.us/perldoc')->status_is(200);

# ベーシック認証においてリクエストのために相対URLを使用する
$t->get_ok('//sri:secr3t@/secrets.json')
  ->status_is(200)
  ->json_is('/1/content', 'Mojo rocks!');

# トランザクションの追加の試験をする
$t->get_ok('/foo')->status_is(200);
is $t->tx->res->dom->at('input')->{value}, 'whatever', 'right value';</pre>

<h2><a class='u'
name="head_ok"
>head_ok</a></h2>

<pre>$t = $t->head_ok('http://example.com/foo');
$t = $t->head_ok('/foo');
$t = $t->head_ok('/foo' => {Accept => '*/*'} => 'Content!');
$t = $t->head_ok('/foo' => {Accept => '*/*'} => form => {a => 'b'});
$t = $t->head_ok('/foo' => {Accept => '*/*'} => json => {a => 'b'});</pre>

<p><code>HEAD</code> リクエストを実行し、成功するかどうかをチェックします。 <a href="/Mojo::UserAgent.html">Mojo::UserAgent</a>の<code>head</code>と同じ引数を受け取ります。</p>

<h2><a class='u'
name="header_is"
>header_is</a></h2>

<pre>$t = $t->header_is(ETag => '"abc321"');
$t = $t->header_is(ETag => '"abc321"', 'right header');</pre>

<p>レスポンスヘッダが完全に一致するかどうかをチェックします。</p>

<h2><a class='u'
name="header_isnt"
>header_isnt</a></h2>

<pre>$t = $t->header_isnt(Etag => '"abc321"');
$t = $t->header_isnt(ETag => '"abc321"', 'different header');</pre>

<p><code>header_is</code>の否定です。</p>

<h2><a class='u'
name="header_like"
>header_like</a></h2>

<pre>$t = $t->header_like(ETag => qr/abc/);
$t = $t->header_like(ETag => qr/abc/, 'right header');</pre>

<p>レスポンスヘッダが類似マッチするかどうかをチェックします。</p>

<h2><a class='u'
name="header_unlike"
>header_unlike</a></h2>

<pre>$t = $t->header_unlike(ETag => qr/abc/);
$t = $t->header_unlike(ETag => qr/abc/, 'different header');</pre>

<p><code>header_like</code>の否定です。</p>

<h2><a class='u'
name="json_has"
>json_has</a></h2>

<pre>$t = $t->json_has('/foo');
$t = $t->json_has('/minibar', 'has a minibar');</pre>

<p><a href="/Mojo::JSON::Pointer.html">Mojo::JSON::Pointer</a>で与えられたJSONポインターを使って、 JSONレスポンスに識別される値が含まれているかどうかをチェックします。</p>

<h2><a class='u'
name="json_hasnt"
>json_hasnt</a></h2>

<pre>$t = $t->json_hasnt('/foo');
$t = $t->json_hasnt('/minibar', 'no minibar');</pre>

<p><code>json_has</code>の否定。</p>

<h2><a class='u'
name="json_is"
>json_is</a></h2>

<pre>$t = $t->json_is({foo => [1, 2, 3]});
$t = $t->json_is('/foo' => [1, 2, 3]);
$t = $t->json_is('/foo/1' => 2, 'right value');</pre>

<p><a href="/Mojo::JSON::Pointer.html">Mojo::JSON::Pointer</a>で与えられたJSONポインタを使って、 JSONレスポンスから抽出された値をチェックします。 JSONポインタが省略された場合は、ルート(<code>/</code>)になります。</p>

<h2><a class='u'
name="json_like"
>json_like</a></h2>

<pre>$t = $t->json_like('/foo/1' => qr/^\d+$/);
$t = $t->json_like('/foo/1' => qr/^\d+$/, 'right value');</pre>

<p><a href="/Mojo::JSON::Pointer.html">Mojo::JSON::Pointer</a>で 与えられたJSONポインタを使って、 JSONレスポンスから抽出された値を、類似チェックします。</p>

<h2><a class='u'
name="json_message_has"
>json_message_has</a></h2>

<pre>$t = $t->json_message_has('/foo');
$t = $t->json_message_has('/minibar', 'has a minibar');</pre>

<p><a href="/Mojo::JSON::Pointer.html">Mojo::JSON::Pointer</a>によって与えられたJSONポインタをを使って、識別される値がJSON WebSocketメッセージに含まれているかどうかをチェックします。</p>

<h2><a class='u'
name="json_message_hasnt"
>json_message_hasnt</a></h2>

<pre>$t = $t->json_message_hasnt('/foo');
$t = $t->json_message_hasnt('/minibar', 'no minibar');</pre>

<p><code>json_message_has</code>の否定。</p>

<h2><a class='u'
name="json_message_like"
>json_message_like</a></h2>

<pre>$t = $t->json_message_like('/foo/1' => qr/^\d+$/);
$t = $t->json_message_like('/foo/1' => qr/^\d+$/, 'right value');</pre>

<p><a href="/Mojo::JSON::Pointer.html">Mojo::JSON::Pointer</a>によって 与えられたJSONポインタを使用して、 JSON WebSocketメッセージから抽出された値の類似チェックします。</p>

<h2><a class='u'
name="json_message_unlike"
>json_message_unlike</a></h2>

<pre>$t = $t->json_message_unlike('/foo/1' => qr/^\d+$/);
$t = $t->json_message_unlike('/foo/1' => qr/^\d+$/, 'different value');</pre>

<p><code>json_message_like</code>の反対。</p>

<h2><a class='u'
name="json_message_is"
>json_message_is</a></h2>

<pre>$t = $t->json_message_is({foo => [1, 2, 3]});
$t = $t->json_message_is('/foo' => [1, 2, 3]);
$t = $t->json_message_is('/foo/1' => 2, 'right value');</pre>

<p><a href="/Mojo::JSON::Pointer.html">Mojo::JSON::Pointer</a>によって与えられたJSONポインタをを使って、 JSON WebSocketメッセージから抽出された値をチェックします。 JSONポインタが省略された場合は、ルート(<code>/</code>)になります。</p>

<h2><a class='u'
name="json_message_like"
>json_message_like</a></h2>

<pre>$t = $t->json_message_like('/foo/1' => qr/^\d+$/);
$t = $t->json_message_like('/foo/1' => qr/^\d+$/, 'right value');</pre>

<p><a href="/Mojo::JSON::Pointer.html">Mojo::JSON::Pointer</a>によるJSONポインタを使って、JSON WebSocketメッセージから抽出された値の類似性を確認します。</p>

<h2><a class='u'
name="json_message_unlike"
>json_message_unlike</a></h2>

<pre>$t = $t->json_message_unlike('/foo/1' => qr/^\d+$/);
$t = $t->json_message_unlike('/foo/1' => qr/^\d+$/, 'different value');</pre>

<p>"json_message_like"の反対。</p>

<h2><a class='u'
name="json_unlike"
>json_unlike</a></h2>

<pre>$t = $t->json_unlike('/foo/1' => qr/^\d+$/);
$t = $t->json_unlike('/foo/1' => qr/^\d+$/, 'different value');</pre>

<p>"json_like"の反対。</p>

<h2><a class='u'
name="message_is"
>message_is</a></h2>

<pre>$t = $t->message_is({binary => $bytes});
$t = $t->message_is({text   => $bytes});
$t = $t->message_is('working!');
$t = $t->message_is('working!', 'right message');</pre>

<p>WebSocketメッセージが完全に一致するかをチェックします。</p>

<h2><a class='u'
name="message_isnt"
>message_isnt</a></h2>

<pre>$t = $t->message_isnt({binary => $bytes});
$t = $t->message_isnt({text   => $bytes});
$t = $t->message_isnt('working!');
$t = $t->message_isnt('working!', 'different message');</pre>

<p><code>message_is</code>の否定。</p>

<h2><a class='u'
name="message_like"
>message_like</a></h2>

<pre>$t = $t->message_like({binary => qr/$bytes/});
$t = $t->message_like({text   => qr/$bytes/});
$t = $t->message_like(qr/working!/);
$t = $t->message_like(qr/working!/, 'right message');</pre>

<p>WebSocketメッセージが類似マッチするかどうかをチェックします。</p>

<h2><a class='u'
name="message_ok"
>message_ok</a></h2>

<pre>$t = $t->message_ok;
$t = $t->message_ok('got a message');</pre>

<p>次のWebSocketメッセージが到着するのを待ちます。</p>

<pre># メッセージを待って、複数の試験を実行する
$t->websocket_ok('/time')
  ->message_ok
  ->message_like(qr/\d+/)
  ->message_unlike(qr/\w+/)
  ->finish_ok;</pre>

<h2><a class='u'
name="message_unlike"
>message_unlike</a></h2>

<pre>$t = $t->message_unlike({binary => qr/$bytes/});
$t = $t->message_unlike({text   => qr/$bytes/});
$t = $t->message_unlike(qr/working!/);
$t = $t->message_unlike(qr/working!/, 'different message');</pre>

<p><code>message_like</code>の否定。</p>

<h2><a class='u'
name="new"
>new</a></h2>

<pre>my $t = Test::Mojo->new;
my $t = Test::Mojo->new('MyApp');
my $t = Test::Mojo->new('MyApp', {foo => 'bar'});
my $t = Test::Mojo->new(Mojo::File->new('/path/to/myapp.pl'));
my $t = Test::Mojo->new(Mojo::File->new('/path/to/myapp.pl'), {foo => 'bar'});
my $t = Test::Mojo->new(MyApp->new);
my $t = Test::Mojo->new(MyApp->new, {foo => 'bar'});</pre>

<p>新しい[Test::Mojo]]オブジェクトを生成します。加えて、クラス名か、アプリケーションスクリプトを示した<a href="/Mojo::File.html">Mojo::File</a>オブジェクトと、アプリケーションの設定を上書きした設定の値をハッシュリファレンスで渡すことができます。特別な設定値である<code>config_override</code>は、<a href="/Mojolicious.html">Mojolicious</a>の"config"を同様に設定し、<a href="/Mojolicious::Plugin::Config.html">Mojolicious::Plugin::Config</a>と<a href="/Mojolicious::Plugin::JSONConfig.html">Mojolicious::Plugin::JSONConfig</a>による設定を、試験のために無効にします。</p>

<pre># "t"ディレクトリへのアプリケーションスクリプトの相対パスから読み込み
use Mojo::File 'path';
my $t = Test::Mojo->new(path(__FILE__)->dirname->sibling('myapp.pl'));</pre>

<h2><a class='u'
name="options_ok"
>options_ok</a></h2>

<pre>$t = $t->options_ok('http://example.com/foo');
$t = $t->options_ok('/foo');
$t = $t->options_ok('/foo' => {Accept => '*/*'} => 'Content!');
$t = $t->options_ok('/foo' => {Accept => '*/*'} => form => {a => 'b'});
$t = $t->options_ok('/foo' => {Accept => '*/*'} => json => {a => 'b'});</pre>

<p><code>OPTIONS</code> リクエストを実行し、成功するかどうかをチェックします。 <a href="/Mojo::UserAgent.html">Mojo::UserAgent</a>の<code>options</code>とまったく同じ引数を受け取ります。</p>

<h2><a class='u'
name="or"
>or</a></h2>

<pre>$t = $t->or(sub {...});</pre>

<p><code>success</code>の値が偽であれば、コールバックを実行します。</p>

<pre># 詳細を出力
$t->get_ok('/bad')->or(sub { diag 'Must have been Glen!' })
  ->status_is(200)->or(sub { diag $t->tx->res->dom->at('title')->text });</pre>

<h2><a class='u'
name="patch_ok"
>patch_ok</a></h2>

<pre>$t = $t->patch_ok('http://example.com/foo');
$t = $t->patch_ok('/foo');
$t = $t->patch_ok('/foo' => {Accept => '*/*'} => 'Content!');
$t = $t->patch_ok('/foo' => {Accept => '*/*'} => form => {a => 'b'});
$t = $t->patch_ok('/foo' => {Accept => '*/*'} => json => {a => 'b'});</pre>

<p><code>PATCH</code> リクエストを実行し、成功するかどうかをチェックします。 <a href="/Mojo::UserAgent.html">Mojo::UserAgent</a>の<code>patch</code>とまったく同じ引数を受け取ります。</p>

<h2><a class='u'
name="post_ok"
>post_ok</a></h2>

<pre>$t = $t->post_ok('http://example.com/foo');
$t = $t->post_ok('/foo');
$t = $t->post_ok('/foo' => {Accept => '*/*'} => 'Content!');
$t = $t->post_ok('/foo' => {Accept => '*/*'} => form => {a => 'b'});
$t = $t->post_ok('/foo' => {Accept => '*/*'} => json => {a => 'b'});</pre>

<p><code>POST</code> リクエストを実行し、成功するかどうかをチェックします。 <a href="/Mojo::UserAgent.html">Mojo::UserAgent</a>の<code>post</code>とまったく同じ引数を受け取ります。</p>

<h2><a class='u'
name="post_form_ok"
>post_form_ok</a></h2>

<pre>$t = $t->post_form_ok('/foo' => {a => 'b'});
$t = $t->post_form_ok('/foo' => 'UTF-8' => {a => 'b'} => {DNT => 1});</pre>

<p><code>POST</code> フォームを送信し、成功するかどうかをチェックします。 <a href="/Mojo::UserAgent.html">Mojo::UserAgent</a>の<code>post_form</code>とまったく同じ引数を受け取ります。</p>

<pre># ファイルアップロードの試験
my $upload = {foo => {content => 'bar', filename => 'baz.txt'}};
$t->post_ok('/upload' => form => $upload)->status_is(200);

# JSON APIの試験
$t->post_json_ok('/hello.json' => json => {hello => 'world'})
  ->status_is(200)
  ->json_is({bye => 'world'});</pre>

<h2><a class='u'
name="put_ok"
>put_ok</a></h2>

<pre>$t = $t->put_ok('http://example.com/foo');
$t = $t->put_ok('/foo');
$t = $t->put_ok('/foo' => {Accept => '*/*'} => 'Content!');
$t = $t->put_ok('/foo' => {Accept => '*/*'} => form => {a => 'b'});
$t = $t->put_ok('/foo' => {Accept => '*/*'} => json => {a => 'b'});</pre>

<p><code>PUT</code> リクエストを実行し、成功するかどうかをチェックします。 <a href="/Mojo::UserAgent.html">Mojo::UserAgent</a>の<code>put</code>とまったく同じ引数を受け取ります。</p>

<h2><a class='u'
name="request_ok"
>request_ok</a></h2>

<pre>$t = $t->request_ok(Mojo::Transaction::HTTP->new);</pre>

<p>リクエストを実行し、透過的なエラーのためのチェックを行います。</p>

<pre># カスタムメソッドによるリクエスト
my $tx = $t->ua->build_tx(FOO => '/test.json' => json => {foo => 1});
$t->request_ok($tx)->status_is(200)->json_is({success => 1});

# カスタムクッキーでリクエスト
my $tx = $t->ua->build_tx(GET => '/account');
$tx->req->cookies({name => 'user', value => 'sri'});
$t->request_ok($tx)->status_is(200)->text_is('head > title' => 'Hello sri');

# カスタムWebSocketハンドシェイク
my $tx = $t->ua->build_websocket_tx('/foo');
$tx->req->headers->remove('User-Agent');
$t->request_ok($tx)->message_ok->message_is('bar')->finish_ok;</pre>

<h2><a class='u'
name="reset_session"
>reset_session</a></h2>

<pre>$t = $t->reset_session;</pre>

<p>ユーザーエージェントのセッションをリセットします。</p>

<h2><a class='u'
name="send_ok"
>send_ok</a></h2>

<pre>$t = $t->send_ok({binary => $bytes});
$t = $t->send_ok({text   => $bytes});
$t = $t->send_ok({json   => {test => [1, 2, 3]}});
$t = $t->send_ok([$fin, $rsv1, $rsv2, $rsv3, $op, $payload]);
$t = $t->send_ok($chars);
$t = $t->send_ok($chars, 'sent successfully');</pre>

<p>WebSocketメッセージあるいはフレームを送信します。</p>

<pre># JSONオブジェクトをテキストメッセージとして送信
$t->websocket_ok('/echo.json')
  ->send_ok({json => {test => 'I ♥ Mojolicious!'}})
  ->message_ok
  ->json_message_is('/test' => 'I ♥ Mojolicious!')
  ->finish_ok;</pre>

<h2><a class='u'
name="status_is"
>status_is</a></h2>

<pre>$t = $t->status_is(200);
$t = $t->status_is(200, 'right status');</pre>

<p>レスポンスのステータスが完全に一致するかをチェックします。</p>

<h2><a class='u'
name="status_isnt"
>status_isnt</a></h2>

<pre>$t = $t->status_isnt(200);
$t = $t->status_isnt(200, 'different status');</pre>

<p><code>status_is</code>の否定です。</p>

<h2><a class='u'
name="text_is"
>text_is</a></h2>

<pre>$t = $t->text_is('div.foo[x=y]' => 'Hello!');
$t = $t->text_is('html head title' => 'Hello!', 'right title');</pre>

<p><a href="/Mojo::DOM.html">Mojo::DOM</a> の<code>at</code>を使って、CSS3 セレクタと最初にマッチする XML/HTML 要素の テキスト内容が完全に一致するかどうかをチェックします。</p>

<h2><a class='u'
name="text_isnt"
>text_isnt</a></h2>

<pre>$t = $t->text_isnt('div.foo[x=y]' => 'Hello!');
$t = $t->text_isnt('html head title' => 'Hello!', 'different title');</pre>

<p><code>text_is</code>の否定です。</p>

<h2><a class='u'
name="text_like"
>text_like</a></h2>

<pre>$t = $t->text_like('div.foo[x=y]' => qr/Hello/);
$t = $t->text_like('html head title' => qr/Hello/, 'right title');</pre>

<p><a href="/Mojo::DOM.html">Mojo::DOM</a>の<code>at</code>を使って、CSS3 セレクタと最初にマッチする XML/HTML 要素の テキスト内容が類似マッチするかどうかをチェックします。</p>

<h2><a class='u'
name="text_unlike"
>text_unlike</a></h2>

<pre>$t = $t->text_unlike('div.foo[x=y]' => qr/Hello/);
$t = $t->text_unlike('html head title' => qr/Hello/, 'different title');</pre>

<p><code>text_like</code>の否定です。</p>

<h2><a class='u'
name="websocket_ok"
>websocket_ok</a></h2>

<pre>$t = $t->websocket_ok('http://example.com/echo');
$t = $t->websocket_ok('/echo');
$t = $t->websocket_ok('/echo' => {DNT => 1} => ['v1.proto']);</pre>

<p>透過的なハンドシェイクでWebSocket接続がオープンするかをチェックします。 <a href="/Mojo::UserAgent.html">Mojo::UserAgent</a>の<code>websocket</code>とまったく同じ引数を受け取ります。</p>

<pre># permessage-deflate圧縮のついたWebSocket
$t->websocket_ok('/' => {'Sec-WebSocket-Extensions' => 'permessage-deflate'})
  ->send_ok('y' x 50000)
  ->message_ok
  ->message_is('z' x 50000)
  ->finish_ok;</pre>

<h1><a class='u'
name="_"
>参考</a></h1>

<p><a href="/Mojolicious.html">Mojolicious</a>, <a href="/Mojolicious::Guides.html">Mojolicious::Guides</a>, <a href="http://mojolicio.us" class="podlinkurl"
>http://mojolicio.us</a>.</p>

<p>(Mojolicious 8.12を反映。2019年6月15日更新)</p>

<!-- end doc -->

</body></html>

  </div>
  <div class="bottom">
    <!-- bottom -->

  </div>
</div>

        </div>
        <div class="side">
          <!-- side -->
<div class="side-list">
  <div class="side-list-title">
    Side Bar
  </div>
  <ul>
    <li><a href="/list.html">Entries</a></li>
    <li>Bar</li>
    <li>Baz</li>
  </ul>
</div>

        </div>
      </div>
      <div class="footer">
        <!-- footer -->
<a href="https://github.com/yuki-kimoto/giblog">Giblog</a>

      </div>
    </div>
  </body>
</html>
